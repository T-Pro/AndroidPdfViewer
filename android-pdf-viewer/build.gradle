plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'com.kezong.fat-aar'
}

android {
    compileSdkVersion 35
    namespace 'com.github.barteksc.pdfviewer'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 1
        versionName "4.0.4"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation 'androidx.core:core:1.3.1'
    embed project(path: ':pdfium', configuration: 'default')
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.github.T-Pro'
                artifactId = 'AndroidPdfViewer'
                version = '4.0.4'

                from components.release

                pom {
                    name = 'AndroidPdfViewer'
                    description = 'Android view for displaying PDFs rendered with PdfiumAndroid'
                    url = 'https://github.com/T-Pro/AndroidPdfViewer'

                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'T-Pro'
                            name = 'T-Pro'
                            email = 'pedro.amorim@tpro.io'
                        }
                    }

                    scm {
                        connection = 'https://github.com/T-Pro/AndroidPdfViewer.git'
                        developerConnection = 'https://github.com/T-Pro/AndroidPdfViewer.git'
                        url = 'https://github.com/T-Pro/AndroidPdfViewer'
                    }
                }
            }
        }
    }
}

afterEvaluate {
    def explodeTasks = tasks.matching { it.name.toLowerCase().startsWith("explode") }

    tasks.matching { it.name.startsWith("extractDeepLinks") }.configureEach { extractTask ->
        explodeTasks.configureEach { explodeTask ->
            extractTask.dependsOn(explodeTask)
        }
    }
}