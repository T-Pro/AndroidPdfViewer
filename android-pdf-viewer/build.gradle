apply plugin: 'com.android.library'
apply plugin: 'com.kezong.fat-aar'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 35
    namespace 'com.github.barteksc.pdfviewer'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 1
        versionName "4.0.13"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation 'androidx.core:core:1.3.1'
    embed project(':pdfium')
}

afterEvaluate {
    // Task dependencies for fat-aar
    tasks.findByName('extractDeepLinksDebug')?.dependsOn(tasks.findByName('explodeCom.github.marain87Pdfium-debug.aarDebug'))
    tasks.findByName('extractDeepLinksRelease')?.dependsOn(tasks.findByName('explodeCom.github.marain87Pdfium-release.aarRelease'))

    // Create AAR artifact lazily
    def aarFile = file("$buildDir/outputs/aar/${project.name}-release.aar")

    // Register the artifact without checking existence during configuration
    def aarArtifact = artifacts.add('archives', aarFile) {
        type 'aar'
        builtBy 'assembleRelease'
    }

    // Publishing configuration
    publishing {
        publications {
            aar(MavenPublication) {
                groupId = 'com.github.T-Pro'
                artifactId = 'AndroidPdfViewer'
                version = '4.0.13'

                // Use the lazy artifact
                artifact aarArtifact
            }
        }
        repositories {
            mavenLocal()
        }
    }
}