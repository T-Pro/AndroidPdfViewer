apply plugin: 'com.android.library'
apply plugin: 'com.kezong.fat-aar'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 35
    namespace 'com.github.barteksc.pdfviewer'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 1
        versionName "4.0.7"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation 'androidx.core:core:1.3.1'
    //api project(':pdfium') use this to run the sample module
    embed project(path: ':pdfium', configuration: 'default')
}

afterEvaluate {
    def bundleReleaseLintTask = tasks.findByName('bundleReleaseLocalLintAar')
    def mergeJarsReleaseTask = tasks.findByName('mergeJarsRelease')
    def bundleDebugLintTask = tasks.findByName('bundleDebugLocalLintAar')
    def mergeJarsDebugTask = tasks.findByName('mergeJarsDebug')

    if (bundleReleaseLintTask && mergeJarsReleaseTask) {
        bundleReleaseLintTask.dependsOn(mergeJarsReleaseTask)
        bundleReleaseLintTask.mustRunAfter(mergeJarsReleaseTask)
    }

    if (bundleDebugLintTask && mergeJarsDebugTask) {
        bundleDebugLintTask.dependsOn(mergeJarsDebugTask)
        bundleDebugLintTask.mustRunAfter(mergeJarsDebugTask)
    }

    def debugEmbedTask = tasks.findByName('explodeCom.github.marain87Pdfium-debug.aarDebug')
    def releaseEmbedTask = tasks.findByName('explodeCom.github.marain87Pdfium-release.aarRelease')

    if (debugEmbedTask) {
        tasks.named('extractDeepLinksDebug').configure {
            dependsOn(debugEmbedTask)
        }
    }

    if (releaseEmbedTask) {
        tasks.named('extractDeepLinksRelease').configure {
            dependsOn(releaseEmbedTask)
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            aar(MavenPublication) {
                groupId = 'com.github.T-Pro'
                artifactId = 'AndroidPdfViewer'
                version = '4.10.0'

                // path to the assembled AAR produced by assembleRelease
                def aarFile = file("$buildDir/outputs/aar/${project.getName()}-release.aar")
                if (!aarFile.exists()) {
                    throw new GradleException("AAR not found: ${aarFile}. Run :${project.path}:assembleRelease first.")
                }
                artifact(aarFile)
            }
        }

        repositories {
            mavenLocal()
        }
    }
}