apply plugin: 'com.android.library'
apply plugin: 'com.kezong.fat-aar'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 35
    namespace 'com.github.barteksc.pdfviewer'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 1
        versionName "4.0.11"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation 'androidx.core:core:1.3.1'
    // we embed pdfium (fat-aar) â€” on JitPack we publish pdfium first to mavenLocal
    embed project(path: ':pdfium', configuration: 'default')
}

afterEvaluate {
    // keep task wiring for fat-aar if needed
    def debugEmbedTask = tasks.findByName('explodeCom.github.marain87Pdfium-debug.aarDebug')
    def releaseEmbedTask = tasks.findByName('explodeCom.github.marain87Pdfium-release.aarRelease')

    if (debugEmbedTask) {
        tasks.named('extractDeepLinksDebug').configure {
            dependsOn(debugEmbedTask)
        }
    }
    if (releaseEmbedTask) {
        tasks.named('extractDeepLinksRelease').configure {
            dependsOn(releaseEmbedTask)
        }
    }

    // Robust publishing: pick the assembled *.aar from build/outputs/aar and publish that file
    publishing {
        publications {
            aar(MavenPublication) {
                groupId = 'com.github.T-Pro'
                artifactId = 'AndroidPdfViewer'
                version = '4.0.11'

                def aarDir = file("$buildDir/outputs/aar")
                def aarFiles = fileTree(dir: aarDir).matching { include('*.aar') }.files
                if (aarFiles.isEmpty()) {
                    throw new GradleException("No AAR found in ${aarDir}. Run :${project.path}:assembleRelease first.")
                }
                def aarFile = aarFiles.iterator().next()
                println "Publishing AAR file: ${aarFile}"
                artifact(aarFile)
            }
        }

        repositories {
            // publish to the JitPack container's local maven repo during CI build
            mavenLocal()
        }
    }
}
